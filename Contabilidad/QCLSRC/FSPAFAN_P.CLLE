PGM &NomProc
    dcl  &NomProc  *char  10
    dcl  &Numreg   *dec  (10 0) 
    dcl  &NUMAPU   *char   6

    Call NOEXISTE
    /* Proceso que copia los ficheros Necesarios  */
    Call CONTAB100 Parm('FSPAFAN')

    /*--------------------------------*/
    /* To Do FSPAFAN                  */
    /*--------------------------------*/
    CRTPF FILE(PARALELOC/ASIPAFAN) SRCFILE(FICHEROS/QDDSSRC) + 
          SRCMBR(ASIFILEN) TEXT('Asientos FSPAFAN') +
          OPTION(*NOSRC *NOLIST) SIZE(*NOMAX) +
          LVLCHK(*NO) AUT(*ALL)
    MONMSG MSGID(CPF0000) EXEC(CLRPFM FILE(PARALELOC/ASIPAFAN))

    CRTPF FILE(PARALELOC/DETEPAFA) SRCFILE(FICHEROS/QDDSSRC) +
          SRCMBR(DETEVI) OPTION(*NOSRC *NOLIST) SIZE(*NOMAX) +
          LVLCHK(*NO) AUT(*ALL)
    MONMSG MSGID(CPF0000) EXEC(CLRPFM FILE(PARALELOC/DETEPAFA))

    CRTPF FILE(PARALELOC/CABEPAFA) SRCFILE(FICHEROS/QDDSSRC) +
          SRCMBR(CABEVI) OPTION(*NOSRC *NOLIST) LVLCHK(*NO) AUT(*ALL)
    MONMSG MSGID(CPF0000) EXEC(CLRPFM FILE(PARALELOC/CABEPAFA))

    /*------------------------------------------------------*/
    /*OVRDBF     FILE(ASIFILEN) TOFILE(PARALELOC/ASIPAFAN)*/
    CALL       PGM(PARALELOC/FSPAFAN) +
               PARM(&NomProc          +
                    'ASIPAFAN'        +
                    'CABEPAFA'        +
                    'DETEPAFA'        +
                    &NUMAPU)                 
    /*DLTOVR FILE(ASIFILEN)*/
    /*------------------------------------------------------*/

    RTVMBRD    FILE(PARALELOC/ASIPAFAN) NBRCURRCD(&NUMREG)
    IF COND(&NUMREG > 0) THEN(DO)

      CPYF  FROMFILE(PARALELOC/DETEPAFA) TOFILE(PARALELOC/DETEVI) +
            MBROPT(*ADD) 
      MONMSG MSGID(CPF0000)

      CPYF  FROMFILE(PARALELOC/CABEPAFA) TOFILE(PARALELOC/CABEVI) +
            MBROPT(*ADD) CRTFILE(*YES) 
      MONMSG MSGID(CPF0000)

      /*-------------------------------------------------------*/
      /* Esta copia al historico debe eliminarse en produccion */
      /*-------------------------------------------------------*/
      CPYF  FROMFILE(PARALELOC/ASIPAFAN) TOFILE(PARALELOC/ASI_HISTO) +
            MBROPT(*ADD) CRTFILE(*YES) 
      MONMSG MSGID(CPF0000)
      /*-------------------------------------------------------*/

    ENDDO

    /*-------------------------------------------------------*/
    /* Actualiza Auditoria Paralelo                          */
    /*-------------------------------------------------------*/
    Call CONTAB101 Parm(&NomProc      +
                    'ASIPAFAN'        +
                    'CABEPAFA'        +
                    'DETEPAFA'        +
                    &NUMAPU           +
                    'FSPAFAN')
    /*--------------------------------*/
    /* Fin del FSPAFAN New            */
    /*--------------------------------*/    
    DSPJOBLOG  OUTPUT(*OUTFILE) OUTFILE(LJMONTERO/FSPAFANJOB)
endpgm